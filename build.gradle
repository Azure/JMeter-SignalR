import java.nio.file.Paths

plugins {
    id 'java'
}

group 'jmeter.signalr'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

//sourceCompatibility = '8'
//targetCompatibility = '8'

dependencies {

    implementation('org.apache.jmeter:ApacheJMeter_core:5.4.3') {
        transitive = false
        //  exclude group: 'org.apache.jmeter', module: 'bom'
    }

    implementation('org.apache.jmeter:ApacheJMeter_components:5.4.3') {
        transitive = false
    }

    implementation('org.apache.jmeter:jorphan:5.4.3') {
        transitive = false
    }

    implementation('com.microsoft.signalr:signalr:7.0.4')

    implementation 'org.slf4j:slf4j-api:2.0.9'
}

jar {
    archiveFileName = 'JmeterSignalR.jar'

    // Include everything by default
    include '**/*'

    // Exclude all META-INF files except MANIFEST.MF
    exclude 'META-INF/*'
    include 'META-INF/MANIFEST.MF'

    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        // Already included in JMeter engine
        exclude 'org/**'
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task publishToJMeterLocal(dependsOn: jar) {
    doLast {
        def jmeterHome = System.getenv('JMETER_HOME')

        def jmeterExtPath = Paths.get(jmeterHome, "lib", "ext")

        def jmeterExtDir = jmeterExtPath.toAbsolutePath().toString()

        copy {
            from "build/libs"
            into jmeterExtDir
            include "JmeterSignalR.jar"
        }
    }
}